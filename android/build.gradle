plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

android {
    compileSdk 34
    namespace "org.ligi.blexplorer"

    defaultConfig {
        applicationId "org.ligi.blexplorer"
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 29
        versionCode 14
        versionName "2.0"
        archivesBaseName = "BLExplorer-$versionName"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    buildFeatures {
        viewBinding true
        prefab true
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // hack for instrumentation testing :-(
        exclude 'LICENSE.txt'

        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'

    }

    sourceSets {
        debug {
            manifest.srcFile 'src/androidTest/AndroidManifest.xml'
        }
    }

    flavorDimensions "build"

    productFlavors {
        dev {
            dimension "build"
            resConfigs "en"
        }

        prod {
            dimension "build"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation "androidx.activity:activity-ktx:1.9.3"
    implementation 'androidx.fragment:fragment-ktx:1.8.5'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.polidea.rxandroidble2:rxandroidble:1.12.1"

    androidTestImplementation 'androidx.annotation:annotation:1.9.1'
    androidTestImplementation 'androidx.appcompat:appcompat:1.7.0'
    androidTestImplementation 'com.google.android.material:material:1.12.0'

    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.6.1') {
        // http://stackoverflow.com/questions/30578243/why-would-adding-espresso-contrib-cause-an-inflateexception
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

    androidTestImplementation('androidx.test.espresso:espresso-web:3.6.1')
    androidTestImplementation 'com.squareup.assertj:assertj-android:1.1.1'
    androidTestImplementation 'org.mockito:mockito-core:2.25.0'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.squareup.assertj:assertj-android:1.1.1'
    testImplementation 'androidx.annotation:annotation:1.9.1'
    testImplementation 'org.mockito:mockito-core:2.25.0'

    implementation 'de.cketti.share:share-intent-builder:0.0.2'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.uber.autodispose:autodispose:1.0.0'
    implementation 'com.uber.autodispose:autodispose-android:1.0.0'
    implementation 'com.uber.autodispose:autodispose-android-archcomponents:1.0.0'

    implementation 'com.jakewharton.rx2:replaying-share:2.0.0'
    implementation 'com.jakewharton.rx2:replaying-share-kotlin:2.0.0'

    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation "androidx.multidex:multidex:2.0.1"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'

    implementation "com.google.dagger:hilt-android:2.51.1"
    kapt "com.google.dagger:hilt-compiler:2.51.1"
}

kapt {
    correctErrorTypes true
}
